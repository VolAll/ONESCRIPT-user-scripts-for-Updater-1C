#use "updater1c"

// ****************************************************************************
// Переменные модуля
// ****************************************************************************

Перем connector;	// Коннектор для подключения к базе.
Перем v8;			// Само подключение к базе через коннектор.
Перем updater;		// Обновлятор, через который мы получаем информацию о базе,
					// а также вызываем различные функции обновлятора.
					// Справка по этому объекту здесь: http://helpme1c.ru

// ****************************************************************************
// Ваш код для выполнения обновлятором
// ****************************************************************************

Процедура Главная()

	Запрос = v8.NewObject("Запрос");
	 
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК Приход,
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК Расход
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|		ДАТАВРЕМЯ(2013, 01, 01), ДАТАВРЕМЯ(2013, 12, 31),
	|		Год, Счет.Код = ""51"", , , , 
	|	) КАК ХозрасчетныйОбороты";
	 
	Результат = Запрос.Выполнить();
	 
	Выборка = Результат.Выбрать();
	 
	ПутьКФайлу = "C:\Аудит.txt";
	 
	Файл = Новый Файл(ПутьКФайлу);
	Если НЕ Файл.Существует() Тогда
		Документ = Новый ТекстовыйДокумент;
		Документ.Записать(ПутьКФайлу, КодировкаТекста.ANSI);
	КонецЕсли;
	 
	Если Выборка.Следующий() Тогда
	 
		Документ = Новый ТекстовыйДокумент;
		Документ.Прочитать(ПутьКФайлу, КодировкаТекста.ANSI);	
	 
		Сообщение = "пришло " + Строка(Выборка.Приход) + " рублей";
		Сообщение = Сообщение + ", ушло " + Строка(Выборка.Расход) + " рублей";
	 
		Документ.ДобавитьСтроку("Обороты по 51 счёту в " + updater.BaseName + " за 2013 год: " + Сообщение);
		Документ.Записать(ПутьКФайлу, КодировкаТекста.ANSI);
	 
	КонецЕсли;

КонецПроцедуры

// ****************************************************************************
// Служебные процедуры
// ****************************************************************************

Процедура ПриНачалеРаботы()

	updater = Новый Updater1C;

	// Если в скрипте не планируется использовать
	// подключение к базе - просто закомментируйте
	// две нижние строки.
	connector = Новый COMОбъект("V" + updater.PlatformRelease + ".COMConnector");
	v8 = updater.BaseConnect(connector);
	
КонецПроцедуры

Процедура ПередЗавершениемРаботы()

	Если connector <> Неопределено Тогда
		ОсвободитьОбъект(connector);
		connector = Неопределено;
	КонецЕсли;
	
	Если v8 <> Неопределено Тогда
		ОсвободитьОбъект(v8);
		v8 = Неопределено;
	КонецЕсли;
	
	ВыполнитьСборкуМусора();
	
КонецПроцедуры

// ****************************************************************************
// Инициализация и запуск скрипта
// ****************************************************************************

ПриНачалеРаботы();

БылиОшибки = Ложь;
Попытка	
	Главная();	
Исключение
	БылиОшибки = Истина;
	Сообщить("<span class='red'><b>" + ОписаниеОшибки() + "</b></span>");
КонецПопытки;

ПередЗавершениемРаботы();

Если БылиОшибки Тогда
	ЗавершитьРаботу(1);
КонецЕсли;